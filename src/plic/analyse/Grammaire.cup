package plic.analyse ;

import java.util.*;
import plic.arbre.* ;
import plic.arbre.expression.* ;
import plic.arbre.instruction.*;
import plic.arbre.classe.*;
import plic.tds.*;
import plic.exceptions.*;

import java_cup.runtime.*;

action code {: 
             :} ;

parser code {:

    public void report_error(String message, Object info) {

        HashMap<Integer, String> lesTerminaux = new HashMap<>() ;
    
        lesTerminaux.put(new Integer(CodesLexicaux.PLUS), "+") ;
        lesTerminaux.put(new Integer(CodesLexicaux.MOINS), "-") ;
        lesTerminaux.put(new Integer(CodesLexicaux.MULT), "*") ;
        lesTerminaux.put(new Integer(CodesLexicaux.DIV), "/") ;
        lesTerminaux.put(new Integer(CodesLexicaux.PAROUV), "(") ;
        lesTerminaux.put(new Integer(CodesLexicaux.PARFER), ")") ;
        lesTerminaux.put(new Integer(CodesLexicaux.SUP), ">") ;
        lesTerminaux.put(new Integer(CodesLexicaux.INF), "<") ;
        lesTerminaux.put(new Integer(CodesLexicaux.EGALEGAL), "==") ;
        lesTerminaux.put(new Integer(CodesLexicaux.DIFF), "!=") ;
        lesTerminaux.put(new Integer(CodesLexicaux.ET), "et") ;
        lesTerminaux.put(new Integer(CodesLexicaux.OU), "ou") ;
        lesTerminaux.put(new Integer(CodesLexicaux.NON), "non") ;

        StringBuffer m = new StringBuffer() ;

        if (info instanceof java_cup.runtime.Symbol) {
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);

            if (s.left >= 0) {                
                m.append("\tligne : " + (s.left + 1)) ;
                if (s.right >= 0)                    
                    m.append(" colonne : " + (s.right+1)) ;
            }
            
            if (s.value != null) {
                lesTerminaux.put(CodesLexicaux.CONSTANTEINT, "" + s.value) ;
                lesTerminaux.put(CodesLexicaux.CONSTANTEBOOL, "" + s.value) ;
            }

            if (lesTerminaux.containsKey(new Integer(s.sym))) {
                m.append(" dernier token lu : " + lesTerminaux.get(new Integer(s.sym))) ;
            }
            else {
                m.append(" expression non terminﾃｩe") ;
            }

        }
        throw new AnalyseSyntaxiqueException("" + m) ;
    }

    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
    }
:};
   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
/* Terminals (tokens returned by the scanner).  */

terminal Symbol    PLUS, MOINS, MULT, DIV, PAROUV, PARFER ;
terminal Symbol    SUP, INF, EGALEGAL, DIFF, ET, OU, NON ;
terminal Symbol    debut, fin, pointvirgule, publique, privee, classe, egal, virgule, ecrire, pour, tantque, repeter, fintantque, si, alors, sinon, fsi, herite, nouveau;
terminal String    CONSTANTEINT, CONSTANTEBOOL, idf, entier, chaine, operoper;

/* Non terminals used in the grammar section.  */

non terminal ArbreAbstrait SYSTEME ;
non terminal Classe CLASSE;
non terminal Expression EXPR, CSTE, INSTANCIATION;
non terminal Integer STATUT;
non terminal Type TYPE;
non terminal List<Instruction> LISTE_DECL, DECL_CHAMP, DECL_CONST, DECLARATION;
non terminal List<Classe> LISTE_CLASSE;
non terminal Instruction INSTRUCTION, ECRIRE, BOUCLE, SI, CONDITION;
non terminal Affectation AFFECT;
non terminal Entree ACCES;
non terminal List<String> LISTE_IDF;

non terminal BlocDInstructions BLOCDINSTRUCTIONS, LIST_INST;

precedence left INF, SUP ;
precedence left EGALEGAL, DIFF;
precedence left PLUS, MOINS;
precedence left MULT, DIV ;
precedence left OU ;
precedence left ET ;
precedence left NON ;

/* WARNING : ne fait que supprimer les erreurs de shift/reduce. pas forcément correct */
precedence left ecrire;
precedence left idf;
precedence left tantque;
precedence left pour;
precedence left si;

/* ----------------------------Grammar Section-------------------- */
 
start with SYSTEME ;

SYSTEME ::= 	LISTE_CLASSE:li
                {:
                       RESULT = li.get(0);  
                :}
				;
				
LISTE_CLASSE    ::= LISTE_CLASSE:li CLASSE:c
				{:
					 if(c != null){
                                li.add(c);
                        }
                        RESULT = li;
				:}
				|CLASSE:c
				{:
					List<Classe> li = new LinkedList<Classe>();
                        if(c != null){
                                li.add(c);
                        }
                        RESULT = li;
				:}
				;
				
CLASSE 	::=	classe idf:i LISTE_DECL:l fin
            {: 
                EntreeClasse e = new EntreeClasse(i);
                Classe c = new Classe(ileft, e);
                c.ajouterTout(l);
                RESULT = c;
            :}
			|classe idf:i herite idf:h LISTE_DECL:l fin
			{:
                                EntreeClasse e = new EntreeClasse(i);
				Classe c = new Classe(ileft, e, h);
                c.ajouterTout(l);
                RESULT = c;
			:}
            ;

LISTE_DECL ::= LISTE_DECL:li DECLARATION:d
                {:
                        if(d != null){
                                li.addAll(d);
                        }
                        RESULT = li;
                :}
                | DECLARATION:d
                {:
                        List<Instruction> li = new LinkedList<Instruction>();
                        if(d != null){
                                li.addAll(d);
                        }
                        RESULT = li;
                :}
                ;
				
DECLARATION ::= DECL_CHAMP
                {:
                        RESULT = null;
                :}
                | DECL_CONST:li
                {:
                        RESULT = li;
                :}
                ;

DECL_CHAMP ::= 	STATUT:s TYPE:t LISTE_IDF:lidf pointvirgule
                {:
                        Tds tds = Tds.getInstance();
                        for(String idf : lidf){
                                Entree e = new Entree(idf);
                                Symbole symb = new Symbole(s, new TypeEntier());
                                tds.ajouter(e, symb);
                                //System.out.println("ajout de variable : "+e.getNom());
                        }
                :}
                ;

LISTE_IDF 	::= LISTE_IDF:li virgule idf:i
                {:
                        li.add(i);
                        RESULT = li;
                :}
                | idf:i
                {:
                        List<String> li = new LinkedList<String>();
                        li.add(i);
                        RESULT = li;
                :}
                ; 



STATUT	::=	publique
		{: RESULT = Symbole.PUBLIQUE; :}
		| privee
		{: RESULT = Symbole.PRIVEE; :}
        ;

TYPE	::=	entier
		{: RESULT = new TypeEntier(); :}
        ;

DECL_CONST ::= 	DECL_CONST:li INSTRUCTION:i
                {:
                        li.add(i);
                        RESULT = li;
                :}
                | INSTRUCTION:i
                {:
                        List<Instruction> li = new LinkedList<Instruction>();
                        li.add(i);
                        RESULT = li;
                :}
                ;

INSTRUCTION ::= AFFECT:a pointvirgule
                {:
                        RESULT = a;
                :}
                | ECRIRE:e pointvirgule
                {:
                		//System.out.println("ecrire");
                        RESULT = e;
                :}
                | BOUCLE:b
                {:
                	RESULT = b;
                :}
                | CONDITION:c fsi
                {:
                	RESULT = c;
                :}
                ;
                
BLOCDINSTRUCTIONS ::=	LIST_INST:li
						{:
							//System.out.println("Bloc D'instructions");
							RESULT = li;
						:}
					;

LIST_INST ::=	LIST_INST:li INSTRUCTION:i
				{:
					li.ajouter(i);
					RESULT = li;
				:}
			|	INSTRUCTION:i
				{:
					BlocDInstructions b = new BlocDInstructions(ileft);
					b.ajouter(i);
					RESULT = b;
				:}
			;
			
BOUCLE 	::=		tantque:ligne EXPR:e repeter BLOCDINSTRUCTIONS:b fintantque
				{:
					//System.out.println("boucle");
					TantQue tq = new TantQue(ligneleft, e, b);
					
					RESULT = tq;
				:}
				|pour PAROUV AFFECT:a pointvirgule EXPR:e2 pointvirgule idf:i1 operoper:op PARFER debut LIST_INST:instr fin
				{:
					RESULT = new Pour(aleft, a, e2, new Entree(i1), op, instr);
				:}
		;
		
CONDITION	::=	SI:s
				{:
					RESULT = s;
				:}
			;


SI	::=	si EXPR:e alors BLOCDINSTRUCTIONS:i
		{:
				//System.out.println("Si");
				RESULT= new Si(e,i,null);
		:}
		| si EXPR:r alors BLOCDINSTRUCTIONS:j sinon BLOCDINSTRUCTIONS:k
		{:
				//System.out.println("Sinon");
				RESULT= new Si(r,j,k);
		:}
		;

AFFECT 	::= 	ACCES:var egal EXPR:e
                {:
                        RESULT = new AffectationEntier(var, e);
                :}
                ;

ACCES	::= 	idf:i
                {: 
                        Entree e = new Entree(i);
                        RESULT = e;
                :}
                ;
				
ECRIRE	::= 	ecrire chaine:cst
                {:
                        RESULT = new Ecrire(0, cst);
                :}
                |ecrire EXPR:e
                {:
                		RESULT = new EcrireExpression(0, e);
                :}
                ;
 
				
EXPR    ::=     EXPR:g  PLUS  EXPR:d
                {: RESULT = new Plus(g, d) ; :}
        | 
                EXPR:g  MOINS  EXPR:d
                {: RESULT = new Moins(g, d); :}
        |                
                EXPR:g  MULT  EXPR:d
                {: RESULT = new Mult(g, d); :}
        |                
                EXPR:g  DIV  EXPR:d
                {: RESULT = new Div(g, d); :}
        |       
                EXPR:g  INF  EXPR:d
                {: RESULT = new Inferieur(g, d); :}
        |       
                EXPR:g  SUP  EXPR:d
                {: RESULT = new Superieur(g, d); :}
        |              
                EXPR:g  EGALEGAL  EXPR:d
                {: RESULT = new Egal(g, d); :}
        |       
                EXPR:g  DIFF  EXPR:d
                {: RESULT = new Different(g, d); :}
        |                      
                MOINS  EXPR:e  
                {: RESULT = new MoinsUnaire(e); :}
        |      
                EXPR:g  ET  EXPR:d
                {: RESULT = new EtLogique(g, d); :}
        |      
                EXPR:g  OU  EXPR:d
                {: RESULT = new OuLogique(g, d); :}
        |      
                NON EXPR:e
                {: RESULT = new NonLogique(e); :}
        |
                CSTE:c
                {: RESULT = c ; :}
        |
                PAROUV  EXPR:e  PARFER
                {: RESULT = e ; :}
        |		
        	ACCES:e
                {: RESULT = new VariableEntiere(0, e) ; :}
        |       
                INSTANCIATION:i
                {: 
                        RESULT = i;
                :}
                ;


INSTANCIATION   ::=     nouveau:n idf:i PAROUV PARFER
                        {:
                                EntreeConstructeur e = new EntreeConstructeur(i);
                                RESULT = new Instanciation(nleft, e);
                        :}
                        ;

CSTE    ::=     CONSTANTEINT:e 
                {: RESULT = new ConstanteEntiere(e, eleft+1) ; :}
        |       CONSTANTEBOOL:e 
                {: RESULT = new ConstanteBool(e, eleft+1) ; :}
                ;